// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = '1.4.0'

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

ext {
    minSdkVersion = 23
    targetSdkVersion = 29
    compileSdkVersion = 29
    buildToolsVersion = '29.0.2'

    // Library versions
    supportLibraryVersion = '1.0.0'
    retrofitVersion = '2.9.0'
    daggerVersion = '2.22.1'

    // Java 8 Compatibility
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    // Build performance
    preDexLibraries = !project.hasProperty('disablePreDex')
    // add 'gradlew [...] -PdisablePreDex' to make CI builds faster
}

allprojects {
    repositories {
        google()
        jcenter()
    }

    // Increase the number of compilation errors displayed from the default of 100.
    // Useful with databinding where the actual cause of a compilation error may not
    // display because every reference to BR, etc becomes invalid due to one error.
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xmaxerrs" << "1000"
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}


subprojects {
    project.plugins.whenPluginAdded { plugin ->
        if ("com.android.build.gradle.AppPlugin" == plugin.class.name) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibraries
        } else if ("com.android.build.gradle.LibraryPlugin" == plugin.class.name) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibraries
        }
    }
}